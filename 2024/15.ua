Step ← (
  ◡(∈:⊓+⊚⊙⊙⋅∘)
  ⨬(◡(∈:⊓+⊚⊙⊙∘)
    ⨬(+ # Neither wall nor block
    | ◡⋅⋅⊚
      +◡⋅⊙∘
      ⍢(+◡⋅⋅∘|∈:)
      ◡⊙⋅⋅⋅⋅∘
      ⊡
      ⨬(⊙⋅+
        ⊙⟜⍜⊡⋅0 # Toggle first block off
        ⊙⍜⊡⋅1: # and last block on
      | ⋅⋅◌    # Would push into wall - do nothing
      )
    )
  | ⋅⊙⊙∘ # Bot hits wall - do nothing
  )
)

$ ##########
$ #..O..O.O#
$ #......O.#
$ #.OO..O.O#
$ #..O@..O.#
$ #O#..O...#
$ #O..O..O.#
$ #.OO.O.OO#
$ #....O...#
$ ##########
$ 
$ <vv>^<v^>v>^vv^v>v<>v^v<v<^vv<<<^><<><>>v<vvv<>^v^>^<<<><<v<<<v^vv^v>^
$ vvv<<^>^v^^><<>>><>^<<><^vv^^<>vvv<>><^^v>^>vv<>v<<<<v<^v>^<^^>>>^<v<v
$ ><>vv>v^v^<>><>>>><^^>vv>v<^^^>>v^v^<^^>v^^>v^<^v>v<>>v^v^<v>v^^<^^vv<
$ <<v<^>>^^^^>>>v^<>vvv^><v<<<>^^^vv^<vvv>^>v<^^^^v<>^>vvvv><>>v^<<^^^^^
$ ^><^><>>><>^^<<^^v>>><^<v>^<vv>>v>>>^v><>^v><<<<v>>v<v<v>vvv>^<><<>^><
$ ^>><>^v<><^vvv<^^<><v<<<<<><^v<<<><<<^^<v<^^^><^>>^<v^><<<^>>^v<v^v<v^
$ >^>>^v>vv>^<<^v<>><<><<v<<v><>v<^vv<<<>^^v^>^^>>><<^v>>v^v><^^>>^<>vv^
$ <><^^>^^^<><vvvvv^v<v<<>^v<v>v<<^><<><<><<<^^<<<^<<>><<><^^^>^^<>^>v<>
$ ^^>vv<^v^v<vv>^<><v<^v>^^^>>>^^vvv^>vvv<>>>^<^>>>>>^<<^v>^vvv<>^<><<v>
$ v^^>>><<^^<>>^v^<v^vv<>v^<<>^<^v^v><^<<<><<^<v><v<>vv>>v><v^<vv<>v^<<^
⍣(&fras"../inputs/2024/15.txt"◌)
:°$"_\n\n_"
⊓(⍉⁅⊟°ℂⁿ:i⊗:">v<^"▽⊸≠@\n
| ⊜∘⊸≠@\n
  ⊃($Start °¤⊚=@@
  | $Boxes =@O
  | $Walls =@#)
)
∧Step
⋅⊙◌
/+♭×[100_1]⊚
